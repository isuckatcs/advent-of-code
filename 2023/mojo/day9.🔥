fn part1(sequences: DynamicVector[DynamicVector[Int]]) -> Int:
    var out = 0
    for i in range(0, len(sequences)):
        let vec = sequences[i]
        out += vec[len(vec) - 1]

    return out


fn part2(sequences: DynamicVector[DynamicVector[Int]]) -> Int:
    var out = 0
    for i in range(len(sequences) - 1, -1, -1):
        out = sequences[i][0] - out

    return out


def main():
    var part1Sum = 0
    var part2Sum = 0

    with open("/dev/stdin", "r") as stdin:
        let input = stdin.read()
        let lines = input.split("\n")

        for i in range(0, len(lines)):
            if len(lines[i]) == 0:
                continue

            var sequences = DynamicVector[DynamicVector[Int]]()
            var s = DynamicVector[Int]()

            let nums = lines[i].split(" ")
            for j in range(0, len(nums)):
                s.push_back(atol(nums[j]))

            sequences.push_back(s ^)

            while True:
                var tmp = DynamicVector[Int]()
                var allZeros = True

                let back = sequences[len(sequences) - 1]
                for j in range(0, len(back) - 1):
                    let diff = back[j + 1] - back[j]
                    allZeros = allZeros and diff == 0
                    tmp.push_back(diff)

                if allZeros:
                    break

                sequences.push_back(tmp ^)

            part1Sum += part1(sequences)
            part2Sum += part2(sequences)

    print(part1Sum)
    print(part2Sum)
